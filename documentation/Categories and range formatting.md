# Categories and range formatting in sl-xj-lib-data
Data handling classes

About Xojo version: tested with Xojo 2024 release 4.1 on Mac.

Latest update this document: 2025-04-18


## Introduction 

The purpose is to assign a category for values in a number or integer column.

The source is example_035


For example, we may want to categorise people based on height

- from 0 to 150cm as small
- from 150cm to 195cm as average
- above 195 as tall

## Creating a sample dataset

So, assuming we have a table with a column labeled “size”:

```xojo

var people as new clDataTable("people")
call  people.AddColumn(new clStringDataSerie("Gender"))
call  people.AddColumn(new clNumberDataSerie("Size"))
call people.AddColumn(new clNumberDataSerie("Weight"))

people.AddRow(new Dictionary("gender":"M", "Size":175.0, "Weight": 82.5))
people.AddRow(new Dictionary("gender":"F","Size":162.0, "Weight": 65.7))
…
people.AddRow(new Dictionary("gender":"F","Size":190.0, "Weight": 80.7))

```

Let’s do a first check on the number of rows per gender:


```xojo

var countPerGender as clDataTable = people.Groupby(array("gender"), "CountPeople")

```

The table generated by this groupby operation contains two columns:

- gender
- CountPeople

The table is named “Results”, we may want to give it a better name as follow:

```xojo
countPerGender.Rename("Count Per Gender")
```

## Adding a range formatting

Let’s now apply a class on the size of people in our sample.

We use a range formatting. In order to keep the original values in the output table, we will apply the range formatting on a copy of the size column and name this column “FormattedSize”:


```xojo
var fmtcol as clAbstractDataSerie = people.AddColumn(people.GetColumn("Size").Clone("FormattedSize"))

```
Now, we can apply the range formatting. Note that since range formatting only applies to numbers, we need to alter above code as follow:

```xojo

var fmtcol as clNumberDataSerie = clNumberDataSerie(people.AddColumn(people.GetColumn("Size").Clone("FormattedSize")))

// Define the range formatting

fmtcol.SetStringFormat(new clRangeFormatting("",""))
fmtcol.AddFormattingRange(0,150.0,"small")
fmtcol.AddFormattingRange(150.0,195.0,"average")
fmtcol.AddFormattingRange(195.0,999.99,"tall")

```
In case you wonder, a value falls in a class when value meets the condition:

	low_bound <= value < high_bound

The values from the new column are shown as labels instead of numeric values:

![Alt text](assets/Example_035_A.png?raw=true "")

Remember the column contains numeric values. How can aggregate the table using the categories ?

We create a new column to store the formatted values:

```xojo
call people.AddColumn(people.GetStringColumn("FormattedSize").Rename("Categ"))

```
When GetStringColumn() is called on a clNumberDataSerie() or a clIntegerDataSerie(), the method attempts to convert the values to string while the method does a simple typecasting for other types.

Now we have our categorical variable and we can aggregate to count the number of people per gender falling in each category:


```xojo
var countPerCategory as clDataTable = people.Groupby(array("gender","Categ"), "CountPeople")

```

Et voilà !

## Full source code and results

The full code of example_035 is hereunder.

```xojo


if Describe then
  Description.RemoveAll
  
  Description.Add(CurrentMethodName)
  
  Description.Add("- Apply range formatting to a column")
  Description.Add(" - Count occurence of each category")
  
  return nil
  
end if

log.start_exec(CurrentMethodName)

var people as new clDataTable("people")
call  people.AddColumn(new clStringDataSerie("Gender"))
call  people.AddColumn(new clNumberDataSerie("Size"))
call people.AddColumn(new clNumberDataSerie("Weight"))

people.AddRow(new Dictionary("gender":"M", "Size":175.0, "Weight": 82.5))
people.AddRow(new Dictionary("gender":"F","Size":162.0, "Weight": 65.7))
people.AddRow(new Dictionary("gender":"F","Size":158.0, "Weight": 55.8))
people.AddRow(new Dictionary("gender":"F","Size":152.0, "Weight": 49.5))
people.AddRow(new Dictionary("gender":"M","Size":165.0, "Weight": 62.9))
people.AddRow(new Dictionary("gender":"M","Size":168.0, "Weight": 58.7))
people.AddRow(new Dictionary("gender":"M","Size":173.0, "Weight": 64.6))
people.AddRow(new Dictionary("gender":"F","Size":150.0, "Weight": 53.9))
people.AddRow(new Dictionary("gender":"F","Size":155.0, "Weight": 58.0))
people.AddRow(new Dictionary("gender":"M","Size":185.0, "Weight": 82.9))
people.AddRow(new Dictionary("gender":"F","Size":178.0, "Weight": 71.3))
people.AddRow(new Dictionary("gender":"F","Size":195.0, "Weight": 80.3))
people.AddRow(new Dictionary("gender":"M","Size":148.0, "Weight": 50.4))
people.AddRow(new Dictionary("gender":"F","Size":188.0, "Weight": 79.0))
people.AddRow(new Dictionary("gender":"F","Size":160.0, "Weight": 55.8))
people.AddRow(new Dictionary("gender":"M","Size":175.0, "Weight": 64.8))
people.AddRow(new Dictionary("gender":"M","Size":206.0, "Weight": 84.8))
people.AddRow(new Dictionary("gender":"F","Size":183.0, "Weight": 69.1))
people.AddRow(new Dictionary("gender":"M","Size":180.0, "Weight": 69.8))
people.AddRow(new Dictionary("gender":"F","Size":193.0, "Weight": 79.0))
people.AddRow(new Dictionary("gender":"F","Size":190.0, "Weight": 80.7))


var countPerGender as clDataTable = people.Groupby(array("gender"), "CountPeople")

countPerGender.Rename("Count Per Gender")

var fmtcol as clNumberDataSerie = clNumberDataSerie(people.AddColumn(people.GetColumn("Size").Clone("FormattedSize")))

fmtcol.SetStringFormat(new clRangeFormatting("",""))
fmtcol.AddFormattingRange(0,150.0,"small")
fmtcol.AddFormattingRange(150.0,195.0,"average")
fmtcol.AddFormattingRange(195.0,999.99,"tall")

call people.AddColumn(people.GetStringColumn("FormattedSize").Rename("Categ"))


var countPerCategory as clDataTable = people.Groupby(array("gender","Categ"), "CountPeople")

log.end_exec(CurrentMethodName)

return  array(people, countPerGender, countPerCategory)

```

All the columns in the data table ‘people’

![Alt text](assets/Example_035_X.png?raw=true "")

The table produced by the last group by:

![Alt text](assets/Example_035_Z.png?raw=true "")

